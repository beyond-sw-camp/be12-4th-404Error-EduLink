pipeline {
    agent any

    environment {
        IMAGE_NAME = 'mstar228/edulink-backend'
        IMAGE_TAG = "1.0.${BUILD_NUMBER}"
        KUBE_HOST = 'test@192.0.2.7'
        PATH = "/usr/local/bin:${env.PATH}"
    }

    stages {
        stage('Git Clone') {
            steps {
                echo "Cloning Repository"
                git url: 'https://github.com/beyond-sw-camp/be12-4th-404Error-EduLink.git',
                    branch: "${env.GIT_BRANCH.replace('origin/', '')}"
            }
        }
        stage('Gradle Build') {
            steps {
                echo "Building Project"
                sh '''
                    echo "Add Permission"
                    chmod +x ${WORKSPACE}/backend/gradlew
                    echo "Cleaning previous build"
                    rm -rf ${WORKSPACE}/backend/build
                    echo "Running Gradle build"
                    cd ${WORKSPACE}/backend && ./gradlew bootJar
                '''
            }
        }
        stage('Build Docker Image') {
            steps {
                script {
                    echo "Building Docker Image ${IMAGE_NAME}:${IMAGE_TAG}"
                    docker.build("${IMAGE_NAME}:${IMAGE_TAG}", "${WORKSPACE}/backend")
                }
            }
        }
        stage('Push to Registry') {
            steps {
                script {
                    echo "Pushing Docker Image ${IMAGE_NAME}:${IMAGE_TAG} to Registry"
                    withDockerRegistry([credentialsId: 'DOCKER_HUB']) {
                        docker.image("${IMAGE_NAME}:${IMAGE_TAG}").push()
                    }
                }
            }
        }
        stage('Determine Deploy Color') {
            steps {
                script {
                    // blue와 green 배포의 ready replica 수를 각각 조회합니다.
                    def blueReady = sh(
                        script: "ssh ${KUBE_HOST} 'kubectl get deployment backend-deployment-blue -o jsonpath=\"{.status.readyReplicas}\"' || echo '0'",
                        returnStdout: true
                    ).trim()
                    def greenReady = sh(
                        script: "ssh ${KUBE_HOST} 'kubectl get deployment backend-deployment-green -o jsonpath=\"{.status.readyReplicas}\"' || echo '0'",
                        returnStdout: true
                    ).trim()
                    echo "Blue ready replicas: ${blueReady}"
                    echo "Green ready replicas: ${greenReady}"

                    // 현재 활성화된 배포 색상을 판단합니다.
                    // 둘 다 0이면 기본값으로 blue를 가정합니다.
                    def currentDeployColor = (blueReady && blueReady != '0') ? 'blue' : (greenReady && greenReady != '0') ? 'green' : 'blue'
                    echo "Current deployment color: ${currentDeployColor}"

                    // 현재 활성화된 색상의 반대색을 새로 배포할 색상으로 정합니다.
                    def deployColor = (currentDeployColor == 'blue') ? 'green' : 'blue'
                    def otherColor = (deployColor == 'blue') ? 'green' : 'blue'
                    echo "Deploying ${deployColor} version (scaling down ${otherColor})"

                    env.DEPLOY_COLOR = deployColor
                    env.OTHER_COLOR = otherColor
                }
            }
        }
        stage('Deploy Blue-Green') {
            when {
                expression { env.GIT_BRANCH == 'origin/kms/main' }
            }
            steps {
                script {
                    if (!env.DEPLOY_COLOR) {
                        error "Deployment color not determined!"
                    }

                    def otherDeploymentExists = sh(
                        script: "ssh ${KUBE_HOST} 'kubectl get deployment backend-deployment-${env.OTHER_COLOR} -o jsonpath=\"{.status.readyReplicas}\"'",
                        returnStdout: true
                    ).trim()

                    // 서비스 YAML 파일의 selector 값을 현재 배포할 색상으로 변경합니다.
                    sh """
                        sed -i "s/deployment: .*/deployment: ${DEPLOY_COLOR}/g" ${WORKSPACE}/backend/k8s/backend-svc.yml
                    """

                    // ConfigMap과 서비스를 업데이트합니다.
                    sh """
                        ssh ${KUBE_HOST} 'kubectl apply -f /home/test/backend/k8s/backend-cm.yml'
                        ssh ${KUBE_HOST} 'kubectl apply -f /home/test/backend/k8s/backend-svc.yml'
                    """

                    // 선택한 배포 파일의 이미지 태그를 업데이트 후 전송합니다.
                    sh """
                        sed -i "s/latest/${IMAGE_TAG}/g" ${WORKSPACE}/backend/k8s/backend-deployment-${env.DEPLOY_COLOR}.yml
                    """
                    sh """
                        scp ${WORKSPACE}/backend/k8s/backend-deployment-${env.DEPLOY_COLOR}.yml ${KUBE_HOST}:/home/test/backend/k8s/
                    """

                    // 배포 또는 스케일 조정을 실행합니다.
                    sh """
                        if [ "${otherDeploymentExists}" = "" ]; then
                            ssh ${KUBE_HOST} 'kubectl apply -f /home/test/backend/k8s/backend-deployment-${env.DEPLOY_COLOR}.yml'
                            ssh ${KUBE_HOST} 'kubectl scale deployment backend-deployment-${env.DEPLOY_COLOR} --replicas=2'
                        else
                            ssh ${KUBE_HOST} 'kubectl scale deployment backend-deployment-${env.OTHER_COLOR} --replicas=0'
                            ssh ${KUBE_HOST} 'kubectl apply -f /home/test/backend/k8s/backend-deployment-${env.DEPLOY_COLOR}.yml'
                            ssh ${KUBE_HOST} 'kubectl scale deployment backend-deployment-${env.DEPLOY_COLOR} --replicas=2'
                        fi
                    """

                    sh """
                        ssh ${KUBE_HOST} 'kubectl rollout status deployment/backend-deployment-${env.DEPLOY_COLOR} --timeout=5m || kubectl get pods'
                    """
                }
            }
        }
    }
}
